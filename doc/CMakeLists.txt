# This file is part of P^nMPI.
#
# Copyright (c)
#  2008-2017 Lawrence Livermore National Laboratories, United States of America
#  2011-2017 ZIH, Technische Universitaet Dresden, Federal Republic of Germany
#  2013-2017 RWTH Aachen University, Federal Republic of Germany
#
#
# P^nMPI is free software; you can redistribute it and/or modify it under the
# terms of the GNU Lesser General Public License as published by the Free
# Software Foundation version 2.1 dated February 1999.
#
# P^nMPI is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
# A PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
# details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with P^nMPI; if not, write to the
#
#   Free Software Foundation, Inc.
#   51 Franklin St, Fifth Floor
#   Boston, MA 02110, USA
#
#
# Written by Martin Schulz, schulzm@llnl.gov.
#
# LLNL-CODE-402774

include(GNUInstallDirs)
include(PnMPI_doc)


#
# Options
#
option("BUILD_DOC" "Build HTML documentation" Off)
if (BUILD_DOC)
  option("BUILD_DOC_INTERNAL" "Including private documentation." Off)
endif ()


#
# Generate Doxygen docs.
#
if (BUILD_DOC)
  find_package(Doxygen REQUIRED)


  # Set several configuration variables used by Doxygen depending on the CMake
  # configuration set.
  if (NOT DOXYGEN_DOT_EXECUTABLE)
    set(DOXYGEN_DOT_EXECUTABLE "")
  endif ()

  set(DOXYGEN_DOC_INTERNAL "NO")
  if (BUILD_DOC_INTERNAL)
    set(DOXYGEN_DOC_INTERNAL "YES")
  endif ()

  set(PNMPI_DOXYFILE "${CMAKE_CURRENT_BINARY_DIR}/doxygen.conf")
  configure_file(doxygen.conf.in ${PNMPI_DOXYFILE} @ONLY)


  # Run Doxygen to generate HTML, Latex and manpage-sources. These will be used
  # in later steps or installed directly.
  set(DOXYGEN_HTML_INDEX "${CMAKE_CURRENT_BINARY_DIR}/html/index.html")
  set(DOXYGEN_LATEX "${CMAKE_CURRENT_BINARY_DIR}/latex/refman.tex")

  add_custom_command(OUTPUT ${DOXYGEN_HTML_INDEX} ${DOXYGEN_LATEX}
                     COMMAND ${DOXYGEN_EXECUTABLE} ${PNMPI_DOXYFILE}
                     WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                     MAIN_DEPENDENCY ${PNMPI_DOXYFILE}
                     DEPENDS
                       pnmpi
                       pnmpize
                       commsub-empty commsub-print
                       empty
                       metrics-counter
                         ${PROJECT_SOURCE_DIR}/modules/metrics/doc.c
                       requests
                       sample1 sample2 sample3 sample4
                       status
                       switch-matrix
                       timelapse
                       virtual
                       wait-for-debugger
                     COMMENT "Generating documentation")
  add_custom_target(doc ALL DEPENDS ${DOXYGEN_HTML_INDEX})

  # install documentation
  option("INSTALL_HTML_DOC" "Install the generated HTML documentation." Off)
  if (INSTALL_HTML_DOC)
    install(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/html
            DESTINATION ${CMAKE_INSTALL_DOCDIR})
  endif ()


  # The HTML documentation is nice for web-access, but for a quick overview a
  # single PDF seems to be a better option. If the platform has a working LaTeX
  # PDF compiler, it will be used to compile a single PDF from the LaTeX files
  # generated by Doxygen which will be installed in the documentation install
  # dir.
  find_package(LATEX)
  if (PDFLATEX_COMPILER)
    set(DOXYGEN_LATEX_PDF "${CMAKE_CURRENT_BINARY_DIR}/latex/refman.pdf")
    add_custom_command(OUTPUT ${DOXYGEN_LATEX_PDF}
                       COMMAND ${PDFLATEX_COMPILER} -interaction=batchmode
                               ${DOXYGEN_LATEX} > /dev/null
                       # Note: pdflatex needs to be called twice to get valid
                       #       references in the generated PDF file.
                       COMMAND ${PDFLATEX_COMPILER} -interaction=batchmode
                               ${DOXYGEN_LATEX} > /dev/null
                       WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/latex
                       MAIN_DEPENDENCY ${DOXYGEN_LATEX}
                       COMMENT "Compiling LaTeX documentation")
    add_custom_target(doc-latex ALL DEPENDS ${DOXYGEN_LATEX_PDF})
    add_dependencies(doc doc-latex)

    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/latex/refman.pdf
            DESTINATION ${CMAKE_INSTALL_DOCDIR}
            RENAME manual.pdf)
  endif ()
endif()
